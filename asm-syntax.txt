file       = fundef*
fundef     = "fun" name "(" params? ")" result? funbody?
params     = param ("," param)*
result     = param ("," param)*
param      = name type | type
funbody    = "{" block+ "}"
block      = anonblock | labelblock
anonblock  = blockstmt+
labelblock = name ":" blockstmt*
blockstmt  = operation | assignment
assignment = (reg | local) "=" (operation | arg)
           ; R3 = add R1 R4
           ; x  = add y z
operation  = op arg*
           ; brz R1 end
arg        = reg | local | literal
literal    = intlit
intlit     = "-"? (binlit | declit | hexlit)
binlit     = "0b" ("0" | "1")+
declit     = (0-9)+
hexlit     = "0x" (0-9A-Fa-f)+
